/**
 *  Original Description:
 *
 *     A simple sequence of N processes arranged in a linear pipe:
 *     every process knows only the next in the row and forwards everything to it.
 *
 *     A property Soter can prove is that the mailbox of each `pipe_node`
 *     contains at most 1 message at any given time.
 *
 *     Adapted from [(Kobayashi, Nakade and Yonezawa, 1995)](http://dx.doi.org/10.1007/3-540-60360-3_42)
 */
target C {
    verification: {
        engine: uclid,
        tactic: induction,
        steps: 2
    },
    no-compile: true
};

reactor Source {

    output out:int;
    logical action _timer(1 sec):int;

    @inv(set(out, 1))
    @inv(schedule(_timer, 0))
    reaction(startup, _timer) -> out, _timer {=
        SET(out, 1);
        schedule(_timer, 0);
    =}
}

reactor Sink {
    input in:int;
    reaction(in) {=
        printf("Received after pipeline: %d\n", in->value);
    =}
}

reactor Node {
    
    input  in:int;
    output out:int;
    
    state  count:int;
    state  payload:int;

    logical action send_output:int;

    @inv(payload == in)
    @inv(count == (prev(count) + 1))
    @inv(schedule(send_output, 0)) // Dummy payload
    reaction(in) -> send_output {=
        self->payload = in->value;
        self->count++;
        schedule(send_output, 0);
    =}

    @inv(set(out, 1)) // FIXME: make it reference payload
    @inv(count == (prev(count) - 1))
    reaction(send_output) -> out {=
        SET(out, self->payload);
        self->count--;
    =}
}

//// Both version works.
// @property("sink_receives_in_time_weaker", 
//     source.reaction[1] ==> 
//         [F[sink.reaction[1] /\ within(500 msec)]
//         \/ G[!sink.reaction[1]]])
// @bound("sink_receives_in_time_weaker", 5)
// @property("sink_receives_in_time_weaker", 
//     source.reaction[1] ==> 
//         [!sink.reaction[1]] W [sink.reaction[1] /\ within(500 msec)])
// @bound("sink_receives_in_time_weaker", 5)
//// Main property
@property("at_most_one_message", 
    [node_1.count <= 1
    /\ node_2.count <= 1]
)
// This should not be 1, since it depends on
// the rate of the timer.
// node_1.count <= 1 alone should have a bound of 2.
// Circuits are perfectly synchronous, so a bound of 1
// is enough. But for sparse synchronous systems,
// we need to account for event rate.
//
// We need to have a timer of 900 msec effect to propagate.
@bound("at_most_one_message", 1)
main reactor {
    
    source = new Source();
    sink = new Sink();
    node_1 = new Node();
    node_2 = new Node();

    source.out -> node_1.in after 1 sec;
    node_1.out -> node_2.in after 1 sec;
    node_2.out -> sink.in after 1 sec;
}